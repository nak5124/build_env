# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=dcadec
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='dcadec is a free DTS Coherent Acoustics decoder with support for HD extensions (mingw-w64)'
pkgver=0.0.0.244.41d36b1
pkgrel=1
arch=('any')
url='https://github.com/foo86/dcadec.git'
license=('LGPL')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/foo86/dcadec.git'
        '0001-Makefile-Don-t-set-static-libgcc-on-mingw-systems.patch'
        '0002-Makefile-Install-windows-DLL-to-BINDIR.patch'
        '0003-Makefile-On-windows-install-import-library.patch'
        '0004-Makefile-Add-API-version-suffix-to-DLL-Name.patch'
        '0005-Makefile-Add-install-cut.patch')
sha512sums=('SKIP'
            '80469f7ad04d7c247dda9c56d7aa9771c3b6ddd8aa238604be7b325ae7ac87cbdfa8668d2212adfa69ddaad3bdd7cad631f0b226255fb5cb6b432f66f6dc8de3'
            'ec11e3a5c300f709f8660c411aaafc98982100a346ad28242640103c2147aaa6dbefdb2a24e5d9198e29111f95fa18faccfd2394a9677c305028e049b4b9879f'
            'f2f7b99dcaf6f4b9d1ffd2ed0dd6f61c6eaba6ee7425752f428e864cec135fc6a4c6c6cf001bfc598ba4a8a487b85a59cfba93a5f59d71bc4a286400efbea985'
            '20edead19655da7c3428a3d7acdc28393fb7da8113708e89176e5e860896686e9fbf7c1f511e8c5a65bad3a166b77e05f5026de9fa9bf3cfe0e64a599a845aee'
            '9ae1446ac65d635858afa05f90734990d08096758bf03a24b9a74d98255643d90badd905e2718eeb97c4a79375c6714585d43c69f47e573d0d7497cc55dcd018')

pkgver() {
  cd "${srcdir}"/$_realname
  local _major=$(grep -e '\#define DCADEC_API_VERSION_MAJOR' libdcadec/dca_context.h | awk '{ printf $3 }')
  local _minor=$(grep -e '\#define DCADEC_API_VERSION_MINOR' libdcadec/dca_context.h | awk '{ printf $3 }')
  local _patch=$(grep -e '\#define DCADEC_API_VERSION_PATCH' libdcadec/dca_context.h | awk '{ printf $3 }')
  printf "%s.%s.%s.%s.%s" "${_major}" "${_minor}" "${_patch}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  patch -p1 -i "${srcdir}"/0001-Makefile-Don-t-set-static-libgcc-on-mingw-systems.patch
  patch -p1 -i "${srcdir}"/0002-Makefile-Install-windows-DLL-to-BINDIR.patch
  patch -p1 -i "${srcdir}"/0003-Makefile-On-windows-install-import-library.patch
  patch -p1 -i "${srcdir}"/0004-Makefile-Add-API-version-suffix-to-DLL-Name.patch
  patch -p1 -i "${srcdir}"/0005-Makefile-Add-install-cut.patch
}

build() {
  cd "${srcdir}"/$_realname

  make clean

  CFLAGS+=" ${CPPFLAGS}"

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_NDEBUG=1 \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  make
}

package() {
  cd "${srcdir}"/$_realname

  CFLAGS+=" ${CPPFLAGS}"

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_NDEBUG=1 \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  DESTDIR="${pkgdir}" \
  make install install-cut

  install -Dm644 COPYING.LGPLv2.1 "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
