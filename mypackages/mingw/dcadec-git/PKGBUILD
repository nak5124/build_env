# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=dcadec
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='dcadec is a free DTS Coherent Acoustics decoder with support for HD extensions.'
pkgver=0.0.0.207.2a9186e
pkgrel=1
arch=('any')
url='https://github.com/foo86/dcadec.git'
license=('LGPL')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/foo86/dcadec.git'
        '0001-Makefile-Don-t-set-static-libgcc-on-mingw-systems.patch'
        '0002-Makefile-Install-windows-DLL-to-BINDIR.patch'
        '0003-Makefile-On-windows-install-import-library-and-def-f.patch'
        '0004-Makefile-Install-windows-DLL-with-mode-755.patch'
        '0005-Makefile-Add-API-version-suffix-to-DLL-Name.patch')
sha512sums=('SKIP'
            '8ccc2ac1ea8639d0cee8538884650d31f0e0dd48d04920be16ee121ca0e33d40ec022ad4fe68f9dd1959cefe1fcacbc46edcab06d791bda84b13f07a1ae96bd8'
            '75df1ed6d14a75bee6ba0b5459ad6bbf649732bea211c9e55115fa2aef324142558b52933923f29359ca84e614c3114e6e9f70bbf5ed6b226a99e62b3c611b2c'
            '1f2d7b4be4eaa0b4b25c3d22c884912365165377f74f400a622d864981d8b3711f369d9c62fa788b800eea47e2c964629e8bc13cbfabdc766d7a305790737e7c'
            '60dcd17210f5e9b21e1f373f5041fe3402d7f57fe354e3c18c6b6e1486cc24033aaf030ff3448e52c6603048d491a49f81b37502005983fb74040956f0e9a3e2'
            '946f67a52a64dc82738f5c8208092de581584ae2e97361657ae320e91adb9b93fa35ca8d59bd2d656f47599ed266b9a47792b314316738abd704daeae778efca')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(grep 'VERSION =' Makefile | awk '{ print $3 }')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  patch -p1 -i "${srcdir}"/0001-Makefile-Don-t-set-static-libgcc-on-mingw-systems.patch
  patch -p1 -i "${srcdir}"/0002-Makefile-Install-windows-DLL-to-BINDIR.patch
  patch -p1 -i "${srcdir}"/0003-Makefile-On-windows-install-import-library-and-def-f.patch
  patch -p1 -i "${srcdir}"/0004-Makefile-Install-windows-DLL-with-mode-755.patch
  patch -p1 -i "${srcdir}"/0005-Makefile-Add-API-version-suffix-to-DLL-Name.patch
}

build() {
  cd "${srcdir}"/$_realname

  make clean

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_NDEBUG=1 \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  CFLAGS="${CFLAGS} ${CPPFLAGS}" \
  make
}

package() {
  cd "${srcdir}"/$_realname

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_NDEBUG=1 \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  CFLAGS="${CFLAGS} ${CPPFLAGS}" \
  DESTDIR="${pkgdir}" \
  make install

  install -Dm644 COPYING.LGPLv2.1 "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
