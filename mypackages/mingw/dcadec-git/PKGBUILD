# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=dcadec
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='dcadec is a free DTS Coherent Acoustics decoder with support for HD extensions.'
pkgver=0.0.0.107.082ca35
pkgrel=1
arch=('any')
url='https://github.com/foo86/dcadec.git'
license=('LGPL')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/nak5124/dcadec.git#branch=mingw')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(grep 'VERSION =' Makefile | awk '{ print $3 }')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"/$_realname

  make clean

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  make
}

package() {
  cd "${srcdir}"/$_realname

  PREFIX=$MINGW_LOCAL_PREFIX \
  CONFIG_WINDOWS=1 \
  CONFIG_SHARED=1 \
  DESTDIR="${pkgdir}" \
  make install

  install -Dm644 COPYING.LGPLv2.1 "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
