# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=libogg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-svn"
pkgdesc='Ogg bitstream and framing library (mingw-w64)'
pkgver=1.3.2.r19480
pkgrel=1
arch=('any')
url='http://xiph.org'
license=('BSD')
makedepends=('svn')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'svn+http://svn.xiph.org/trunk/ogg')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(grep AC_INIT configure.in | sed -e "s/^.*,\[\(.*\)\],.*$/\1/")
  printf "%s.r%s" "${_ver}" "$(svnversion | tr -d 'A-z')"
}

prepare() {
  cd "${srcdir}"/$_realname

  msg2 "Running Autoreconf"
  autoreconf -vfis
}

build() {
  cd "${srcdir}"/$_realname

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --build=$MINGW_CHOST \
    --host=$MINGW_CHOST \
    --disable-silent-rules \
    --enable-shared \
    --disable-static \
    --enable-fast-install \
    --with-gnu-ld

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DESTDIR="${pkgdir}" install

  # install BSD license
  install -Dm644 COPYING "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/COPYING
}
