# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=x264-10bit
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='Library and CLI tool for encoding H264/AVC video streams, 10bit-depth (mingw-w64)'
pkgver=148.2648.23b798e
pkgrel=1
arch=('any')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git' 'pkgconf')
depends=("${MINGW_PACKAGE_PREFIX}-l-smash"
         "${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname%-*}"::'git+https://github.com/nak5124/x264.git')
sha512sums=('SKIP')

: << "#__CO__"
_samples="${HOME}/y4m/deadline_cif.y4m \
          ${HOME}/y4m/foreman_cif.y4m  \
          ${HOME}/y4m/soccer_4cif.y4m"
#__CO__

pkgver() {
  cd "${srcdir}"/${_realname%-*}
  local _ver=$(grep '#define X264_BUILD' x264.h | awk '{ print $3}')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname%-*}
}

build() {
  cd "${srcdir}"/${_realname%-*}

  _CFLAGS="${CFLAGS/-fstack-protector-strong --param=ssp-buffer-size=4/}"
  LDFLAGS="${LDFLAGS/-fstack-protector-strong --param=ssp-buffer-size=4/}"
  _LDFLAGS="${LDFLAGS/,--gc-sections/}"
  unset CFLAGS LDFLAGS

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --enable-shared \
    --disable-opencl \
    --enable-win32thread \
    --disable-interlaced \
    --bit-depth=10 \
    --chroma-format=all \
    --host=$MINGW_CHOST \
    --extra-cflags="${_CFLAGS}" \
    --extra-ldflags="${_LDFLAGS}"

  if [ ! -z "${_samples}" ]; then
    make $MAKEFLAGS fprofiled VIDS="${_samples}" PROF_USE_LD+=' -lgcov'
  else
    make $MAKEFLAGS
  fi
}

package() {
  cd "${srcdir}"/${_realname%-*}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname%-*}/LICENSE
}
