# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=openh264
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='A free, open-source codec library for H.264 encoding and decoding (mingw-w64)'
pkgver=1.4.3532.f6ee34c
pkgrel=1
arch=('any')
url='http://www.openh264.org/'
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/cisco/openh264.git#branch=openh264v1.4.1'
        '0001-build-Use-version-script.patch'
        '0002-Add-API-version-suffix-to-DLL-Name.patch')
sha512sums=('SKIP'
            '3804dfd0c06151f5a8ee58078242b2f8dba3c2918d290bee7cdfad9902b827c83f5871648891f53b8a868701e2a6df5b68ac40815c7b71ba00c8e0ee208af303'
            '21492e184b2efc94a240015cb4507b8821084fa3a7e43872d2eaab5cdf31f6352151a6c2689232ef4ceac6ab1975536a9a31c5602ae030173acd987ce0b47781')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(grep '^VERSION=' Makefile | sed 's/VERSION=//g')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname
  msg2 'Applying patches'
  if [ -f "${srcdir}/${_realname}/libopenh264.ver" ]; then
    rm -f "${srcdir}"/${_realname}/libopenh264.ver
  fi
  patch -p1 -i "${srcdir}"/0001-build-Use-version-script.patch
  patch -p1 -i "${srcdir}"/0002-Add-API-version-suffix-to-DLL-Name.patch
}

build() {
  cd "${srcdir}"/$_realname

  if [ "${MINGW_CARCH}" = "x86_64" ]; then
    _arch='x86_64'
  else
    _arch='i386'
  fi

  make clean
  make ARCH=$_arch CFLAGS_OPT="${CPPFLAGS} ${CFLAGS}" LDFLAGS="${CFLAGS} ${LDFLAGS}" $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DESTDIR="${pkgdir}" PREFIX=$MINGW_LOCAL_PREFIX install-shared

  install -Dm644 LICENSE "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
