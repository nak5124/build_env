# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=libvpx
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='The VP8 Codec SDK (mingw-w64)'
pkgver=1.4.0.13377.be380f2
pkgrel=1
arch=('any')
url='http://www.webmproject.org/'
license=('BSD')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://chromium.googlesource.com/webm/libvpx'
        '0001-enable-shared-mingw.patch'
        '0002-fix-exports.mingw.patch'
        '0003-give-preference-win32api-over-pthread.patch')
sha512sums=('SKIP'
            '65ab7ef6c9a484ee4e679fa1b420e46d9e21c3bb5d3dcee9fe63f17cebc10a285be587a052bae32ebed3b6d22e0a41f1717b7d40843d6b48d247cdbcaa877515'
            '69dd57bd50b5dfe37f4e6a7884f39c0b49ea7ca05051dedb51c5be13107866673f55aa5cd42e0d3b39252d77ec78e7ddb0466e11bb4bb3c7d758e8f41eb0ff36'
            '284ea6f9bfe98605fdf1df28f546db212f494968a1e8282e94a3eb1fc53a385d5eb477863a47ea89de5d9643ea0063c66c0ff3ea151ee094a33c9d6efd76c818')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(git describe | awk -F"-" '{ print $1}' | sed 's/v//g')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  patch -p1 -i "${srcdir}"/0001-enable-shared-mingw.patch
  patch -p1 -i "${srcdir}"/0002-fix-exports.mingw.patch
  patch -p1 -i "${srcdir}"/0003-give-preference-win32api-over-pthread.patch
}

build() {
  cd "${srcdir}"/$_realname

  if [ "${MINGW_CARCH}" = "x86_64" ]; then
    local _target=x86_64-win64-gcc
  else
    local _target=x86-win32-gcc
  fi

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --target=$_target \
    --disable-werror \
    --enable-optimizations \
    --disable-debug \
    --disable-install-docs \
    --disable-install-bins \
    --enable-install-libs \
    --disable-install-srcs \
    --enable-libs \
    --disable-examples \
    --disable-docs \
    --disable-unit-tests \
    --as=yasm \
    --disable-codec-srcs \
    --disable-debug-libs \
    --enable-vp9-highbitdepth \
    --enable-vp8 \
    --enable-vp9 \
    --disable-postproc \
    --disable-vp9-postproc \
    --enable-multithread \
    --enable-spatial-resampling \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --disable-static \
    --enable-multi-res-encoding \
    --enable-vp9-temporal-denoising \
    --disable-webm-io \
    --disable-libyuv
  sed -i 's|-O3||g' libs-${_target}.mk

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DIST_DIR="${pkgdir}"${MINGW_LOCAL_PREFIX} install

  sed -i 's|-lpthread||g' "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/pkgconfig/vpx.pc
  install -Dm644 LICENSE "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
