# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=libvpx
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='The VP8/VP9 Codec SDK (mingw-w64)'
pkgver=1.4.0.13515.a4bb5f2
pkgrel=1
arch=('any')
url='http://www.webmproject.org/'
license=('BSD')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://chromium.googlesource.com/webm/libvpx'
        '0001-Support-building-shared-libraries-on-MinGW-w64-GCC.patch'
        '0002-Give-preference-win32api-over-pthread-on-MinGW-w64.patch')
sha512sums=('SKIP'
            '7da7e487fb5df69efd8f47428b0d76ce698e06dd05e8d193b5d428b9e98e786be15558a490afd78f9db24e1bbbc095d896c080a0c7bf3a1d0bf8c594796ff080'
            'd1dd5d5c31b43bef811dadc4853ca1450d16aa95a6f0354a92bbd4da003e6c1efe8ea8e150caced023d6917b58bfa439b0ce37b74c116a8727605fff8884e4be')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(git describe | awk -F"-" '{ print $1}' | sed 's/v//g')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  patch -p1 -i "${srcdir}"/0001-Support-building-shared-libraries-on-MinGW-w64-GCC.patch
  patch -p1 -i "${srcdir}"/0002-Give-preference-win32api-over-pthread-on-MinGW-w64.patch
}

build() {
  cd "${srcdir}"/$_realname

  if [ "${MINGW_CARCH}" = "x86_64" ]; then
    local _target=x86_64-win64-gcc
  else
    local _target=x86-win32-gcc
  fi

  CFLAGS="${CFLAGS} ${CPPFLAGS}"
  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --target=$_target \
    --disable-werror \
    --enable-optimizations \
    --disable-debug \
    --disable-install-docs \
    --disable-install-bins \
    --enable-install-libs \
    --disable-install-srcs \
    --enable-libs \
    --disable-examples \
    --disable-docs \
    --disable-unit-tests \
    --as=yasm \
    --disable-codec-srcs \
    --disable-debug-libs \
    --enable-vp9-highbitdepth \
    --enable-vp8 \
    --enable-vp9 \
    --disable-postproc \
    --disable-vp9-postproc \
    --enable-multithread \
    --enable-spatial-resampling \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --disable-static \
    --enable-multi-res-encoding \
    --enable-vp9-temporal-denoising \
    --disable-webm-io \
    --disable-libyuv
  sed -i 's|-O3||g' libs-${_target}.mk
  sed -i 's|-lpthread||g' libs-${_target}.mk

  make clean
  make $MAKEFLAGS V=1
}

package() {
  cd "${srcdir}"/$_realname
  make DIST_DIR="${pkgdir}"${MINGW_LOCAL_PREFIX} install

  sed -i 's|-lpthread||g' "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/pkgconfig/vpx.pc
  install -Dm644 LICENSE "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
