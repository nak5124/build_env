# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=libvpx
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='The VP8/VP9 Codec SDK (mingw-w64)'
pkgver=1.4.0.14197.9a9b1cf
pkgrel=1
arch=('any')
url='http://www.webmproject.org/'
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://chromium.googlesource.com/webm/libvpx'
        '0001-Support-building-shared-libraries-on-MinGW-w64-GCC.patch'
        '0002-Give-preference-win32api-over-pthread-on-MinGW-w64.patch'
        '0003-Fix-VPXMAX-declaration.patch')
sha512sums=('SKIP'
            'e88262f37b18cecde928f93f4a1129105bbdb72407b29a8450b0c56c709967aa559408d57f0e1ffefa706223f08d372cb554c6b6ccb95e7c41d1fffd7e6be8af'
            '5ebf6a8613096c47ce581e2654dcb0158700d266daea212b62df3e1ae5dae4fcd8bbfd06031771f796d3b93e74de5b26795e97eba0aed29681ca3dc3582b4181'
            '1aa767a5cf3213f8a975b7d8a8102ed4d567d891532c9f23f46615e6f4020a5b347fff0d36787bfb5f693a0be12cefd92fd33eaef3f4e6fc8108dda6221854dc')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(git describe | awk -F"-" '{ print $1}' | sed 's/v//g')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  patch -p1 -i "${srcdir}"/0001-Support-building-shared-libraries-on-MinGW-w64-GCC.patch
  patch -p1 -i "${srcdir}"/0002-Give-preference-win32api-over-pthread-on-MinGW-w64.patch
  patch -p1 -i "${srcdir}"/0003-Fix-VPXMAX-declaration.patch
}

build() {
  cd "${srcdir}"/$_realname

  if [ "${MINGW_CARCH}" = "x86_64" ]; then
    local _target=x86_64-win64-gcc
  else
    local _target=x86-win32-gcc
  fi

  CFLAGS="${CFLAGS} ${CPPFLAGS}"
  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --target=$_target \
    --disable-werror \
    --enable-optimizations \
    --disable-debug \
    --disable-install-docs \
    --disable-install-bins \
    --enable-install-libs \
    --disable-install-srcs \
    --enable-libs \
    --disable-examples \
    --disable-docs \
    --disable-unit-tests \
    --as=yasm \
    --disable-codec-srcs \
    --disable-debug-libs \
    --enable-vp9-highbitdepth \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp10 \
    --disable-postproc \
    --disable-vp9-postproc \
    --enable-multithread \
    --enable-spatial-resampling \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --disable-static \
    --enable-multi-res-encoding \
    --enable-vp9-temporal-denoising \
    --disable-webm-io \
    --disable-libyuv
  sed -i 's|-O3||g' libs-${_target}.mk
  sed -i 's|-lpthread||g' libs-${_target}.mk

  make clean
  make $MAKEFLAGS V=1
}

package() {
  cd "${srcdir}"/$_realname
  make DIST_DIR="${pkgdir}"${MINGW_LOCAL_PREFIX} install

  sed -i 's|-lpthread||g' "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/pkgconfig/vpx.pc
  install -Dm644 LICENSE "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
