# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=liblzma
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='Library for LZMA compressed files (mingw-w64)'
pkgver=5.2.1.1051.a245189
pkgrel=1
arch=('any')
url='http://tukaani.org/xz/'
license=('LGPL')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+http://git.tukaani.org/xz.git')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/$_realname
  local _major=$(grep -e '#define LZMA_VERSION_MAJOR' src/liblzma/api/lzma/version.h | sed -e 's/#define LZMA_VERSION_MAJOR //;s/ //g')
  local _minor=$(grep -e '#define LZMA_VERSION_MINOR' src/liblzma/api/lzma/version.h | sed -e 's/#define LZMA_VERSION_MINOR //;s/ //g')
  local _micro=$(grep -e '#define LZMA_VERSION_PATCH' src/liblzma/api/lzma/version.h | sed -e 's/#define LZMA_VERSION_PATCH //;s/ //g')
  printf "%s.%s.%s.%s.%s" "${_major}" "${_minor}" "${_micro}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  msg2 'Running autogen.sh'
  ./autogen.sh
}

build() {
  cd "${srcdir}"/$_realname

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --build=$MINGW_CHOST \
    --host=$MINGW_CHOST \
    --enable-threads=vista \
    --enable-assume-ram=16384 \
    --disable-xz \
    --disable-xzdec \
    --disable-lzmadec \
    --disable-lzmainfo \
    --disable-lzma-links \
    --disable-scripts \
    --disable-silent-rules \
    --enable-shared \
    --disable-static \
    --enable-fast-install \
    --disable-nls \
    --disable-rpath \
    --enable-unaligned-access \
    --with-gnu-ld

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DESTDIR="${pkgdir}" install
}
