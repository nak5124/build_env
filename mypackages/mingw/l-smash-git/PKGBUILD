# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=l-smash
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='Loyal to Spec of Mpeg4 and Ad-hoc Simple Hackwork. Yet another opensource mp4 handler (mingw-w64)'
pkgver=2.8.4.1352.4f91d83
pkgrel=1
arch=('any')
url='http://l-smash.github.io/l-smash/'
license=('ISC')
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/l-smash/l-smash.git'
        '0000-build-Add-unix-version-script.patch'
        '0001-.gitignore-Add-version-script.patch'
        '0002-configure-Check-whether-SRCDIR-is-git-repo-or-not.patch'
        '0003-configure-Add-api-version-to-mingw-shared-library-na.patch'
        '0004-build-Use-lib.exe-or-dlltool-when-available-on-mingw.patch'
        '0005-configure-If-shared-is-enabled-put-LIBS-on-Libs.priv.patch'
        '0006-Makefile-Move-.ver-from-clean-to-distclean.patch'
        '0007-build-Add-non-public-symbols-which-start-lsmash_-pre.patch')
sha512sums=('SKIP'
            '9bda1848e779e0b1e72e9dc62efe2c92883a57fd25084999c13f7834c492b5d8ae9f9218fc5a51705c848ec10c856eb6be594ccbb143cb8f66bea95fdd6f716d'
            '517a464f7133c044573acf7e4b1009a2a2ade326479c47a309c9659bf411ceab9d62e18be0597dd8c384f6212dc93e5ee28481291d44241d9b10c318f37f1596'
            '0bd97a4720f6ecc857630129b99f2694bc8f304dc326486e8c2da56025616a29dcecf4a53ea8e8f6f8777d4b16582b6cbd6d93f13da38a905e16382fc0e316b2'
            '86787115d133f9e768f5df2ede14b25ee9962f691e44b425726108792777c357fd03a17ecfe59edb7fe12c865348717479e3981191e78285da45f84742dfe4a3'
            '2292e564a4bec4a3d57491e9d6b973273f60e8da4537be58338ab5ba31e16a14f6c1fd4bf658f7e30a17783cf5b098b2272216043cc99577c87c1f12bfe5d9cf'
            '55fb7aea418b22a24d35574409af91ceaac03b7f1a5ff23c8d283bbf8c4ea498ac02b1bdc6de0587c691db2aff0f496c616d3e87fa2ffc3ed1e657ef4d8939aa'
            '19cefaf76d9c573667167e8952af7535a76dc714421ee336fb3e4e52b21cfbdcbc9dafeb60cbd988fce3c8f2b5e2f10b6f394ccb773bd0e2931bae4174f704d7'
            'fdcd5242c4fc49421f1dce3cd5abab8bda670a3dd727fbdd16b27134104781310598ac7422ffe6849b52df37751393296e72e4604cfdfd63b18889211d2b21c2')

pkgver() {
  cd "${srcdir}"/$_realname
  local _major=$(grep -e '#define LSMASH_VERSION_MAJOR' lsmash.h | sed -e 's/#define LSMASH_VERSION_MAJOR //;s/ //g')
  local _minor=$(grep -e '#define LSMASH_VERSION_MINOR' lsmash.h | sed -e 's/#define LSMASH_VERSION_MINOR //;s/ //g')
  local _micro=$(grep -e '#define LSMASH_VERSION_MICRO' lsmash.h | sed -e 's/#define LSMASH_VERSION_MICRO //;s/ //g')
  printf "%s.%s.%s.%s.%s" "${_major}" "${_minor}" "${_micro}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname
  msg2 'Applying patches'
  if [ -f "${srcdir}/${_realname}/liblsmash.v" ]; then
    rm -f "${srcdir}"/${_realname}/liblsmash.v
  fi
  patch -p1 -i "${srcdir}"/0000-build-Add-unix-version-script.patch
  patch -p1 -i "${srcdir}"/0001-.gitignore-Add-version-script.patch
  patch -p1 -i "${srcdir}"/0002-configure-Check-whether-SRCDIR-is-git-repo-or-not.patch
  patch -p1 -i "${srcdir}"/0003-configure-Add-api-version-to-mingw-shared-library-na.patch
  patch -p1 -i "${srcdir}"/0004-build-Use-lib.exe-or-dlltool-when-available-on-mingw.patch
  patch -p1 -i "${srcdir}"/0005-configure-If-shared-is-enabled-put-LIBS-on-Libs.priv.patch
  patch -p1 -i "${srcdir}"/0006-Makefile-Move-.ver-from-clean-to-distclean.patch
  patch -p1 -i "${srcdir}"/0007-build-Add-non-public-symbols-which-start-lsmash_-pre.patch
}

build() {
  cd "${srcdir}"/$_realname

  if [ "${MINGW_CARCH}" = "x86_64" ]; then
    PATH=${PATH}:$VC64_DIR
  else
    PATH=${PATH}:$VC32_DIR
  fi
  export PATH

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --disable-static \
    --enable-shared \
    --extra-cflags="${CFLAGS} ${CPPFLAGS}" \
    --extra-ldflags="${LDFLAGS}"

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DESTDIR="${pkgdir}" install

  # install ISC license
  install -Dm644 LICENSE "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
