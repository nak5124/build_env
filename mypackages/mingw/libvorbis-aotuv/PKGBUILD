# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=libvorbis-aotuv
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc='A fork of libvorbis intended to provide better quality sound at low to medium bitrates (mingw-w64)'
pkgver=b6.03
pkgrel=4
_releasedate=20110424-20150808
_srcrel=2015
arch=('any')
url='http://www.geocities.jp/aoyoume/aotuv'
license=('BSD')
makedepends=('pkgconf')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain"
         "${MINGW_PACKAGE_PREFIX}-libogg")
source=("${url}/source_code/${_realname}_${pkgver}_${_srcrel}.tar.bz2")
sha512sums=('f87e494aad030e1e91a2a49b64c24384af7c5a3e768432859c5b72c4e2b0564fcea3d0787eed61278a99e9624dcc49cfec589d9446bfaec3b88c22040a820a05')

prepare() {
  cd "${srcdir}"/aotuv-${pkgver}_$_releasedate

}

build() {
  cd "${srcdir}"/aotuv-${pkgver}_$_releasedate

  ./autogen.sh \
    --prefix=$MINGW_LOCAL_PREFIX \
    --build=$MINGW_CHOST \
    --host=$MINGW_CHOST \
    --enable-shared \
    --disable-static \
    --enable-fast-install \
    --disable-examples \
    --with-gnu-ld \
    --with-ogg=$MINGW_LOCAL_PREFIX

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/aotuv-${pkgver}_$_releasedate
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"${MINGW_LOCAL_PREFIX}/bin/libvorbisfile-*.dll
  rm -f "${pkgdir}"${MINGW_LOCAL_PREFIX}/include/vorbis/vorbisfile.h
  rm -f "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/libvorbisfile.dll.a
  rm -f "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/pkgconfig/vorbisfile.pc
  rm -fr "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/doc/libvorbis-*/vorbisfile
  install -Dm644 COPYING "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
