# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=mfx_dispatch
pkgbase=mingw-w64-$_realname
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc='Intel Media SDK Dispatched static library (mingw-w64)'
pkgver=1.16.30.1582088
pkgrel=1
arch=('any')
url='https://github.com/lu-zero/mfx_dispatch'
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
source=("${_realname}"::'git+https://github.com/lu-zero/mfx_dispatch.git')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/$_realname
  local _ver=$(grep AC_INIT configure.ac | sed -e "s/^.*, \[\(.*\)\].*$/\1/")
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname

  msg2 'Running Autoreconf'
  autoreconf -vfis
}

build() {
  cd "${srcdir}"/$_realname

  PATH=${MINGW_SYS_PREFIX}/bin:/usr/bin \
  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --build=$MINGW_CHOST \
    --host=$MINGW_CHOST \
    --disable-silent-rules \
    --disable-shared \
    --enable-static \
    --enable-fast-install

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/$_realname
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
