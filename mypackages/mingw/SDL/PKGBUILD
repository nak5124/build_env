# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=SDL
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (mingw-w64)'
pkgver=1.2.15
pkgrel=1
arch=('any')
url='http://www.libsdl.org'
license=('LGPL')
makedepends=('pkgconf')
depends=("${MINGW_PACKAGE_PREFIX}-toolchain")
options=('staticlibs')
source=("${url}/release/${_realname}-${pkgver}.tar.gz"
        '0001-SDL-update-ltmain.sh.patch')
sha512sums=('ac392d916e6953b0925a7cbb0f232affea33339ef69b47a0a7898492afb9784b93138986df53d6da6d3e2ad79af1e9482df565ecca30f89428be0ae6851b1adc'
            '116de87b60c28f2e381bcf9c8fe8a408803903693622ac45372657488f5a96f86cd02645cc6dfb335cfe4f96934e2e76539318cb38aab1bbce4003ece4e18781')

prepare() {
  cd "${srcdir}"/${_realname}-$pkgver

  patch -p1 -i "${srcdir}"/0001-SDL-update-ltmain.sh.patch
  sed -i 's|-mwindows||g' configure
  sed -i 's/enable-auto-image-base/disable-auto-image-base/g' configure
}

build() {
  cd "${srcdir}"/${_realname}-$pkgver

  ./configure \
    --prefix=$MINGW_LOCAL_PREFIX \
    --build=$MINGW_CHOST \
    --host=$MINGW_CHOST \
    --enable-shared \
    --disable-static \
    --enable-fast-install \
    --enable-libc \
    --enable-audio \
    --enable-video \
    --enable-events \
    --enable-joystick \
    --enable-cdrom \
    --enable-threads \
    --enable-timers \
    --enable-file \
    --enable-loadso \
    --enable-cpuinfo \
    --disable-assembly \
    --enable-diskaudio \
    --enable-dummyaudio \
    --enable-video-dummy \
    --enable-video-opengl \
    --enable-stdio-redirect \
    --enable-directx \
    --with-gnu-ld

  make clean
  make $MAKEFLAGS
}

package() {
  cd "${srcdir}"/${_realname}-$pkgver
  make DESTDIR="${pkgdir}" install

  sed -i "s|-mwindows||g" "${pkgdir}"${MINGW_LOCAL_PREFIX}/lib/pkgconfig/sdl.pc
  install -Dm644 COPYING "${pkgdir}"${MINGW_LOCAL_PREFIX}/share/licenses/${_realname}/LICENSE
}
