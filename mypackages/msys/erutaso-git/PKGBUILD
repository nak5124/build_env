# Maintainer: Yuta Nakai <nak5124@live.jp>

pkgname=erutaso-git
pkgdesc='Cure your bad habit of mistyping, えるたそ～'
pkgver=9.ed5b15f
pkgrel=2
arch=('i686' 'x86_64')
url='https://github.com/sgymtic/sl'
license=('Custom')
provides=('erutaso')
conflicts=('erutaso')
makedepends=('git' 'ncurses-devel')
depends=('ncurses')
source=("${pkgname}"::'git+https://github.com/sgymtic/sl.git')
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/$pkgname
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"/$pkgname

  gcc $CFLAGS $CPPFLAGS -I/usr/include/ncursesw -o erutaso sl.c -lncursesw
}

package() {
  cd "${srcdir}"/$pkgname

  install -Dm644 "${srcdir}"/${pkgname}/LICENSE "${pkgdir}"/usr/share/licenses/erutaso/LICENSE
  install -d "${pkgdir}"/usr/bin
  install -m755 "${srcdir}"/${pkgname}/erutaso "${pkgdir}"/usr/bin
}
