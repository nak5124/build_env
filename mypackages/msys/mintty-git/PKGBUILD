# Maintainer: Yuta Nakai <nak5124@live.jp>

pkgname=mintty-git
pkgdesc='Terminal emulator with native Windows look and feel'
pkgver=2.1.5.859.f9db71f
pkgrel=1
arch=('i686' 'x86_64')
url='http://mintty.github.io/'
license=('GPL3')
provides=('mintty')
conflicts=('mintty')
makedepends=('git')
depends=('sh')
source=("${pkgname}"::'git+https://github.com/mintty/mintty.git'
        '0001-undefine-debug_virtual_key_codes.patch')
sha512sums=('SKIP'
            'e6817379f930173499e5bab56cca63d080e956edfe4c8b4ec0a4e562b21e289c9f1accb1993b3e694b10185c45f6e0e3eb40277e470ce04911d3ef1c3e66c092')
pkgver() {
  cd "${srcdir}"/$pkgname
  local _major=$(grep -e '\#define MAJOR_VERSION' src/appinfo.h | awk '{ printf $3 }')
  local _minor=$(grep -e '\#define MINOR_VERSION' src/appinfo.h | awk '{ printf $3 }')
  local _patch=$(grep -e '\#define PATCH_NUMBER' src/appinfo.h | awk '{ printf $3 }')
  printf "%s.%s.%s.%s.%s" "${_major}" "${_minor}" "${_patch}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$pkgname

  patch -p1 -i "${srcdir}"/0001-undefine-debug_virtual_key_codes.patch
}

build() {
  cd "${srcdir}"/$pkgname
  make
}

package() {
  cd "${srcdir}"/$pkgname
  mkdir -p "${pkgdir}"/usr/{bin,share}
  mkdir -p "${pkgdir}"/usr/share/man/man1

  install -m755 bin/mintty.exe "${pkgdir}"/usr/bin/mintty.exe
  install -m644 docs/mintty.1 "${pkgdir}"/usr/share/man/man1/mintty.1
}
