# Maintainer: Yuta Nakai <nak5124@live.jp>

pkgname=tmux-git
pkgver=1.9a.5006.b8aec17
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
provides=('tmux')
conflicts=('tmux')
depends=('ncurses' 'libevent')
makedepends=('git' 'ncurses-devel' 'libevent-devel')
source=("${pkgname}"::'git+http://git.code.sf.net/p/tmux/tmux-code'
        'LICENSE'
        '0001-msys-platform.patch'
        '0002-fix-declaration-of-MAXPATHLEN.patch'
        '0003-use_acs.patch'
        '0004-utf8_width.patch'
        '0005-copy_cursor_width.patch')
sha512sums=('SKIP'
            'b03188cd81ffe7443905bb2cd2bb9330a00ddad258eb0f1ad64add09c2c305f1ad6686855986ce6f828a3884bfbaebbd9896e2307e65a47797a628d81c4e3677'
            '322a9c95dec1bad12a147a7b160ace52ba77a0b9873aa9e5c22f6e1f0f6505f57c99d651a47365d0c469e8461b43f13379a825551bde5a9f750ff4f9964dc81c'
            'c131473dd30ae2d60ac3a83adf33df9655a2fcac1b648ebd08322bbea2fee5eeda002ae6c3542d0adad5d6d5d5ce2d4e00d1e53678d8d6c9b6bad10a40d52804'
            'f7ca3e9c0962d78812baacfc3487a24e705c17b937c3b394fb1b813eb8ba852379d82f0c8a0a5c9adb38c94ac90b92c5711e7b28cbff875c1265855c6f2da55a'
            '41a33b94ab14b0916211e2c6489fc6adb3d487994ed569bc152d3298bd74b34e266ed08da77e396f0641438ee72e5f6ffa479e78ed5575fa5a18c6d476b4a43d'
            '6f9df7888fcd8aaa62deeda6fec319d66de5c8f54247cc671a7b85b79c05e1c84710de235d3a28588b367aa44d47c04e489c2e7daf662670ad7742f686e19c4d')

pkgver() {
  cd "${srcdir}"/$pkgname
  local _ver=$(grep 'RELEASE=' configure.ac | sed 's/RELEASE=//g')
  printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$pkgname

  patch -p1 -i "${srcdir}"/0001-msys-platform.patch
  patch -p1 -i "${srcdir}"/0002-fix-declaration-of-MAXPATHLEN.patch
  patch -p1 -i "${srcdir}"/0003-use_acs.patch
  patch -p1 -i "${srcdir}"/0004-utf8_width.patch
  patch -p1 -i "${srcdir}"/0005-copy_cursor_width.patch
  cp -fa osdep-cygwin.c osdep-msys.c

  msg2 'Running autogen.sh'
  ./autogen.sh
}

build() {
  cd "${srcdir}"/$pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd "${srcdir}"/$pkgname
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/tmux/LICENSE

  install -dm755 "${pkgdir}"/usr/share/tmux/
  install -m644 examples/* "${pkgdir}"/usr/share/tmux/
  install -Dm644 examples/tmux.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/tmux.vim

  install -d "${pkgdir}"/usr/share/bash-completion/completions/
  mv "${pkgdir}"/usr/share/tmux/bash_completion_tmux.sh "${pkgdir}"/usr/share/bash-completion/completions/tmux
}
