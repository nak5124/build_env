# Maintainer: Yuta Nakai <nak5124@live.jp>

pkgname=tmux-git
pkgdesc='A terminal multiplexer'
pkgver=2.0.280.g2c6ea70
pkgrel=2
arch=('i686' 'x86_64')
url='http://tmux.sourceforge.net/'
license=('BSD')
provides=('tmux')
conflicts=('tmux')
makedepends=('git' 'ncurses-devel' 'libevent-devel')
depends=('ncurses' 'libevent')
source=("${pkgname}"::'git+https://github.com/tmux/tmux.git'
        '0001-msys-platform.patch'
        '0002-use_acs.patch'
        '0003-utf8_width.patch'
        '0004-copy_cursor_width.patch'
        '0005-msys-fix-permission-denied.patch')
sha512sums=('SKIP'
            '322a9c95dec1bad12a147a7b160ace52ba77a0b9873aa9e5c22f6e1f0f6505f57c99d651a47365d0c469e8461b43f13379a825551bde5a9f750ff4f9964dc81c'
            'b00fdfe389410c6dba0f66e251e3fd2bb2e92c215d690087fb148056c880ea5c6daa0071b97f47556915595d5145eab4ffabdb1e820e5b9e7c2b9c14bbb27a58'
            '263662c0476854855e2ecd940c99ea9b6b4388040b1860b6f843f58a0202ee950374794678abcef3d2cd5a507a6dae92606ea0107b5c000d2294b7488cba18c4'
            'c40b61f9f6e6432381ab0052d23f3c24a74e4ca1827e21c9da298f0eaaa954ed19e4fd909bbe3d860d9ab1f10aa46f7165270359a368204c5dfa0e88a36782aa'
            '79c8450896778894e1a59d451d5d5ae258f3f9d93c494391cd603b4680c9616d85e0c75a2844c284c497f9c0c70e8deafa21b0ef97a226051bfe409f3b5d124e')

pkgver() {
  cd "${srcdir}"/$pkgname
  git describe --tags | sed -e 's|-|.|g' -e 's|v||g'
}

prepare() {
  cd "${srcdir}"/$pkgname

  patch -p1 -i "${srcdir}"/0001-msys-platform.patch
  patch -p1 -i "${srcdir}"/0002-use_acs.patch
  patch -p1 -i "${srcdir}"/0003-utf8_width.patch
  patch -p1 -i "${srcdir}"/0004-copy_cursor_width.patch
  patch -p1 -i "${srcdir}"/0005-msys-fix-permission-denied.patch
  cp -fa osdep-cygwin.c osdep-msys.c

  msg2 'Running autogen.sh'
  ./autogen.sh
}

build() {
  cd "${srcdir}"/$pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd "${srcdir}"/$pkgname
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/licenses/tmux
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/tmux

  install -d "${pkgdir}"/usr/share/tmux
  install -m 644 examples/* "${pkgdir}"/usr/share/tmux
  install -d "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -m 644 examples/tmux.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax

  install -d "${pkgdir}"/usr/share/bash-completion/completions/tmux
  mv "${pkgdir}"/usr/share/tmux/bash_completion_tmux.sh "${pkgdir}"/usr/share/bash-completion/completions/tmux
}
