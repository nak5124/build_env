# Maintainer: Yuta Nakai <nak5124@live.jp>

pkgname=the_silver_searcher-git
pkgdesc='Code searching tool similar to Ack, but faster'
pkgver=0.31.0.1639.9cd0d78
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
provides=('the_silver_searcher' 'ag')
conflicts=('the_silver_searcher' 'ag')
makedepends=('git' 'liblzma-devel' 'pcre-devel' 'zlib-devel')
depends=('zlib' 'pcre' 'xz')
source=("${pkgname}"::'git+https://github.com/ggreer/the_silver_searcher.git')
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}"/$pkgname
    printf "%s.%s.%s" "$(git describe --tags | awk -F'-' '{ print $1 }')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$pkgname

  msg2 'Running Autoreconf'
  autoreconf -vfis
}

build() {
  cd "${srcdir}"/$pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

package() {
  cd "${srcdir}"/$pkgname
  make DESTDIR="${pkgdir}" install
}
