# Maintainer: Yuta Nakai <nak5124@live.jp>

_realname=l-smash
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Loyal to Spec of Mpeg4 and Ad-hoc Simple Hackwork. Yet another opensource mp4 handler"
pkgver=1.13.2
pkgrel=1
arch=('any')
url="http://l-smash.github.io/l-smash/"
license=('ISC')
#makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/l-smash/l-smash/archive/v${pkgver}.tar.gz"
        '718de728046be264797128691f398dc9e92d5045.patch'
        '0001-configure-Link-static-liblsmash-to-cli-tools-when-st.patch'
        '0002-configure-Check-whether-SRCDIR-is-git-repo-or-not.patch'
        '0003-configure-Add-api-version-to-SHAREDLIBNAME.patch'
        '0004-build-Use-lib.exe-when-it-is-available-on-mingw.patch')
md5sums=('ffcf9da73bf6d4e9aa3c75e7b99faebd'
         'f69aeb5e9093fe8d6c4bf0fb846517e7'
         'dfe9a0cf4635e3001c7bbbe23d8ed346'
         '1bf5f7a70c2df7f91f3415dc18deebae'
         '5f38e452254713a8ea5d1bc1e72226df'
         '3785f4b123407175331b13b60d3125f1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/718de728046be264797128691f398dc9e92d5045.patch
  patch -p1 -i ${srcdir}/0001-configure-Link-static-liblsmash-to-cli-tools-when-st.patch
  patch -p1 -i ${srcdir}/0002-configure-Check-whether-SRCDIR-is-git-repo-or-not.patch
  patch -p1 -i ${srcdir}/0003-configure-Add-api-version-to-SHAREDLIBNAME.patch
  patch -p1 -i ${srcdir}/0004-build-Use-lib.exe-when-it-is-available-on-mingw.patch
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-shared

  make ${MAKEFLAGS}
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
